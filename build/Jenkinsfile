pipeline {
  agent any

  options {
    buildDiscarder(logRotator(numToKeepStr: '60', artifactNumToKeepStr: '5'))
  }

  triggers {
    cron '@midnight'
  }

  parameters {
    string(name: 'engineSource', defaultValue: 'https://product.ivyteam.io', description: 'Engine page url')
    string(name: 'testFilter', defaultValue: 'WebTest*.java', description: 'Change to only run tests of the matching classes (flag will be gnored on master)')
  }

  environment {
    testFilter = getTestFilter()
  }

  stages {
    stage('check editorconfig') {
      steps {
        script {
          docker.image('mstruebing/editorconfig-checker').inside {
            warnError('There are some editor errors') {
              sh 'ec -no-color'
            } 
          }
        }
      }
    }

    stage('build') {
      steps {
        script {
          def common = load 'build/build.groovy'
          def phase = isReleaseOrMasterBranch() ? 'deploy' : 'verify'
          common.buildCockpit(phase, env.testFilter)
        }
      }
    }
  }
}

def getTestFilter() {
  if (isReleaseOrMasterBranch()) {
    return 'WebTest*.java'
  }
  return params.testFilter
}

def isReleaseOrMasterBranch() {
  return env.BRANCH_NAME == 'master' || env.BRANCH_NAME.startsWith('release/') 
}
